# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//build/config/ui.gni")
import("//lib/ui/ozone/ozone.gni")

assert(use_x11 || ozone_platform_x11)

jumbo_component("x11") {
  output_name = "x11_events_platform"

  sources = [
    "x11_event_source.cc",
    "x11_event_source.h",
    "x11_hotplug_event_handler.cc",
    "x11_hotplug_event_handler.h",
  ]

  defines = [ "EVENTS_IMPLEMENTATION" ]

  configs += [ "//build/config/linux:x11" ]

  public_deps = [
    "//lib/ui/base/x",
    "//lib/ui/events",
    "//lib/ui/events:events_base",
    "//lib/ui/events/devices",
    "//lib/ui/events/devices/x11",
    "//lib/ui/events/platform",
    "//lib/ui/events/x",
    "//lib/ui/gfx",
    "//lib/ui/gfx/x",
  ]

  deps = [
    "//lib/base",
  ]

  if (is_chromeos) {
    deps += [ "//lib/ui/events/ozone:events_ozone" ]
  }

  if (use_glib) {
    sources += [
      "x11_event_source_glib.cc",
      "x11_event_source_glib.h",
    ]

    configs += [ "//build/config/linux:glib" ]
  } else {
    sources += [
      "x11_event_source_libevent.cc",
      "x11_event_source_libevent.h",
    ]

    deps += [ "//lib/ui/events/keycodes:x11" ]
  }
}
