# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("test_support") {
  # testonly = true
  sources = [
    "mojo_test_base.cc",
    "mojo_test_base.h",
    "test_utils.h",
    "test_utils_win.cc",
  ]

  if (!is_ios) {
    sources += [
      "multiprocess_test_helper.cc",
      "multiprocess_test_helper.h",
    ]
  }

  if (is_fuchsia) {
    sources += [ "test_utils_fuchsia.cc" ]
  } else if (is_posix) {
    sources += [ "test_utils_posix.cc" ]
  }

  deps = [
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/public/cpp/system",
    "//lib/testing/gtest",
  ]
}

source_set("run_all_unittests") {
  # testonly = true
  sources = [
    "run_all_unittests.cc",
  ]

  deps = [
    ":test_support",
    ":test_support_impl",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/public/c/test_support",
    "//lib/testing/gtest",
  ]

  if (is_linux && !is_component_build) {
    public_configs = [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

source_set("run_all_perftests") {
  # testonly = true
  deps = [
    ":test_support_impl",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/edk/test:test_support",
    "//lib/mojo/public/c/test_support",
  ]

  sources = [
    "run_all_perftests.cc",
  ]

  if (is_linux && !is_component_build) {
    public_configs = [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

static_library("test_support_impl") {
  # testonly = true
  deps = [
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/public/c/test_support",
    "//lib/mojo/public/cpp/system",
  ]

  sources = [
    "test_support_impl.cc",
    "test_support_impl.h",
  ]
}
