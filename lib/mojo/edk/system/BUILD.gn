# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/nacl/config.gni")
import("//lib/mojo/public/tools/bindings/mojom.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("test_utils") {
  # testonly = true

  sources = [
    "test_utils.cc",
    "test_utils.h",
  ]

  public_deps = [
    "//lib/mojo/public/c/system",
    "//lib/mojo/public/cpp/system",
    "//lib/mojo/public/cpp/platform",
  ]

  deps = [
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/edk/test:test_support",
    "//lib/testing/gtest:gtest",
  ]
}

source_set("test_sources") {
  # testonly = true
  sources = [
    "channel_unittest.cc",
    "core_test_base.cc",
    "core_test_base.h",
    "core_unittest.cc",
    "handle_table_unittest.cc",
    "message_pipe_unittest.cc",
    "message_unittest.cc",
    "options_validation_unittest.cc",
    "platform_handle_dispatcher_unittest.cc",
    "shared_buffer_dispatcher_unittest.cc",
    "shared_buffer_unittest.cc",
    "signals_unittest.cc",
    "trap_unittest.cc",
  ]

  if (!is_ios) {
    sources += [
      "data_pipe_unittest.cc",
      "multiprocess_message_pipe_unittest.cc",
      "platform_wrapper_unittest.cc",
    ]
  }

  deps = [
    ":test_utils",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/edk/embedder:embedder_unittests",
    "//lib/mojo/edk/system/ports:tests",
    "//lib/mojo/edk/test:run_all_unittests",
    "//lib/mojo/edk/test:test_support",
    "//lib/mojo/public/cpp/system",
    "//lib/mojo/public/cpp/platform",
    "//lib/testing/gmock",
    "//lib/testing/gtest",
  ]

  allow_circular_includes_from = [ "//lib/mojo/edk/embedder:embedder_unittests" ]
}
