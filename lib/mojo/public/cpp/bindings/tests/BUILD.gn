# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("tests") {
  # testonly = true

  sources = [
    "associated_interface_unittest.cc",
    "bind_task_runner_unittest.cc",
    "binding_callback_unittest.cc",
    "binding_set_unittest.cc",
    "binding_unittest.cc",
    "bindings_test_base.cc",
    "bindings_test_base.h",
    "buffer_unittest.cc",
    "callback_helpers_unittest.cc",
    "connector_unittest.cc",
    "constant_unittest.cc",
    "container_test_util.cc",
    "container_test_util.h",
    "data_view_unittest.cc",
    "equals_unittest.cc",
    "handle_passing_unittest.cc",
    "hash_unittest.cc",
    "interface_ptr_unittest.cc",
    "lazy_serialization_unittest.cc",
    "map_unittest.cc",
    "message_queue.cc",
    "message_queue.h",
    "multiplex_router_unittest.cc",
    "native_struct_unittest.cc",
    "report_bad_message_unittest.cc",
    "request_response_unittest.cc",
    "router_test_util.cc",
    "router_test_util.h",
    "sample_service_unittest.cc",
    "serialization_warning_unittest.cc",
    "struct_unittest.cc",
    "sync_handle_registry_unittest.cc",
    "sync_method_unittest.cc",
    "test_helpers_unittest.cc",
    "type_conversion_unittest.cc",
    "union_unittest.cc",
    "validation_context_unittest.cc",
    "validation_unittest.cc",
    "variant_test_util.h",
  ]

  deps = [
    ":mojo_public_bindings_test_utils",
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/public/cpp/bindings",
    "//lib/mojo/public/cpp/system",
    "//lib/mojo/public/cpp/test_support:test_utils",
    "//lib/mojo/public/interfaces/bindings/tests:test_associated_interfaces",
    "//lib/mojo/public/interfaces/bindings/tests:test_export_component",
    "//lib/mojo/public/interfaces/bindings/tests:test_export_component2",
    "//lib/mojo/public/interfaces/bindings/tests:test_exported_import",
    "//lib/mojo/public/interfaces/bindings/tests:test_interfaces",
    "//lib/mojo/public/interfaces/bindings/tests:test_struct_traits_interfaces",
    "//lib/testing/gtest",
  ]

  data = [
    "//lib/mojo/public/interfaces/bindings/tests/data/validation/",
  ]

  if (is_ios) {
    assert_no_deps = [ "//third_party/WebKit/*" ]
  } else {
    sources += [
      "pickle_unittest.cc",
      "struct_traits_unittest.cc",
    ]

    deps += [
      "//lib/mojo/public/cpp/bindings/tests:struct_with_traits_impl",
      "//lib/mojo/public/interfaces/bindings/tests:test_interfaces_blink",
    ]
  }
}

if (!is_ios) {
  source_set("for_blink_tests") {
    # testonly = true

    sources = [
      "container_test_util.cc",
      "container_test_util.h",
      "variant_test_util.h",
      "wtf_hash_unittest.cc",
      "wtf_map_unittest.cc",
      "wtf_types_unittest.cc",
    ]

    deps = [
      "//lib/mojo/public/cpp/bindings",
      "//lib/mojo/public/cpp/system",
      "//lib/mojo/public/interfaces/bindings/tests:test_export_blink_component",
      "//lib/mojo/public/interfaces/bindings/tests:test_exported_import_blink",
      "//lib/mojo/public/interfaces/bindings/tests:test_interfaces",
      "//lib/mojo/public/interfaces/bindings/tests:test_interfaces_blink",
      "//lib/mojo/public/interfaces/bindings/tests:test_wtf_types",
      "//lib/mojo/public/interfaces/bindings/tests:test_wtf_types_blink",
      "//lib/testing/gtest",
    ]
  }
}

source_set("struct_with_traits_impl") {
  sources = [
    "struct_with_traits_impl.cc",
    "struct_with_traits_impl.h",
  ]

  deps = [
    "//lib/base",
    "//lib/mojo/public/cpp/system:system",
  ]
}

source_set("perftests") {
  # testonly = true

  sources = [
    "bindings_perftest.cc",
  ]

  if (!is_ios) {
    sources += [ "e2e_perftest.cc" ]
  }

  deps = [
    "//lib/base/test:test_support",
    "//lib/mojo/edk",
    "//lib/mojo/edk/test:test_support",
    "//lib/mojo/public/cpp/bindings",
    "//lib/mojo/public/cpp/system",
    "//lib/mojo/public/cpp/test_support:test_utils",
    "//lib/mojo/public/interfaces/bindings/tests:test_interfaces",
    "//lib/testing/gtest",
  ]
}

source_set("mojo_public_bindings_test_utils") {
  sources = [
    "validation_test_input_parser.cc",
    "validation_test_input_parser.h",
  ]

  deps = [
    "//lib/mojo/public/c/system",
  ]
}
