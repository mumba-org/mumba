# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//lib/testing/test.gni")

group("mojo") {
  # Meta-target, don't link into production code.
  # testonly = true
  deps = [
    ":tests",
    "//lib/mojo/common",
  ]

  if (!(is_linux && current_cpu == "x86")) {
    deps += [ "//lib/mojo/public" ]
  }

  if (is_android) {
    deps += [ "//lib/mojo/android" ]
  }

  deps += [ "//lib/services/service_manager:all" ]
}

group("tests") {
  # testonly = true
  deps = [
    ":mojo_perftests",
    ":mojo_unittests",
    "//lib/ipc:ipc_tests",
    "//lib/services/service_manager/tests",
  ]
}

test("mojo_unittests") {
  deps = [
    "//lib/mojo/edk/system:test_sources",
    "//lib/mojo/edk/test:run_all_unittests",
    "//lib/mojo/public/cpp/base:tests",
    "//lib/mojo/public/cpp/bindings/tests",
    "//lib/mojo/public/cpp/platform/tests",
    "//lib/mojo/public/cpp/system/tests",
  ]
}

test("mojo_perftests") {
  deps = [
    "//lib/mojo/edk/test:run_all_perftests",
    "//lib/mojo/edk/test:test_support",
    "//lib/mojo/public/c/system/tests:perftests",
    "//lib/mojo/public/cpp/bindings/tests:perftests",
  ]

  if (!is_ios) {
    sources = [
      "//lib/mojo/edk/system/message_pipe_perftest.cc",
    ]

    deps += [
      "//lib/base",
      "//lib/base/test:test_support",
      "//lib/mojo/edk",
      "//lib/mojo/edk/system:test_utils",
      "//lib/testing/gtest",
    ]
  }
}
