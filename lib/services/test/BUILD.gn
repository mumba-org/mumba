# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

source_set("run_all_service_tests") {
  # This target is in a separate directory because we want to expand DEPS so
  # that it not all of //services can access //ui.
  visibility = [ "//lib/services:*" ]

  # testonly = true

  sources = [
    "run_all_service_tests.cc",
  ]

  deps = [
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/services/service_manager/public/cpp/test:common_initialization",
    "//lib/ui/base",
  ]

  if (is_ios) {
    deps += [ ":tests_bundle_data" ]
  } else {
    data = [
      "data/",
    ]
  }

  data_deps = [
    "//lib/ui/resources:ui_test_pak_data",
  ]

  if (is_android) {
    deps += [ "//lib/mojo/android:libsystem_java" ]
  }

  if (use_ozone) {
    deps += [ "//lib/ui/ozone" ]
  }
}

bundle_data("tests_bundle_data") {
  visibility = [ ":run_all_service_tests" ]
  # testonly = true
  sources = [
    "//lib/services/test/data/content-sniffer-test0.html",
    "//lib/services/test/data/content-sniffer-test0.html.mock-http-headers",
    "//lib/services/test/data/content-sniffer-test1.html",
    "//lib/services/test/data/content-sniffer-test1.html.mock-http-headers",
    "//lib/services/test/data/content-sniffer-test2.html",
    "//lib/services/test/data/content-sniffer-test2.html.mock-http-headers",
    "//lib/services/test/data/content-sniffer-test4.html",
    "//lib/services/test/data/content-sniffer-test4.html.mock-http-headers",
    "//lib/services/test/data/empty.html",
    "//lib/services/test/data/hello.html",
    "//lib/services/test/data/hello.html.mock-http-headers",
    "//lib/services/test/data/nocache.html",
    "//lib/services/test/data/nocache.html.mock-http-headers",
    "//lib/services/test/data/nosniff-test.html",
    "//lib/services/test/data/nosniff-test.html.mock-http-headers",
    "//lib/services/test/data/redirect307-to-echo",
    "//lib/services/test/data/redirect307-to-echo.mock-http-headers",
    "//lib/services/test/data/simple_page.html",
    "//lib/services/test/data/title1.html",
  ]
  outputs = [
    "{{bundle_resources_dir}}/" +
        "{{source_root_relative_dir}}/{{source_file_part}}",
  ]
}
