# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//lib/services/catalog/public/tools/catalog.gni")
import("//lib/services/service_manager/public/cpp/service.gni")
import("//lib/services/service_manager/public/service_manifest.gni")
import("//lib/services/service_manager/public/tools/test/service_test.gni")
import("//lib/testing/test.gni")

source_set("lib") {
  sources = [
    "mus_demo.cc",
    "mus_demo.h",
    "window_tree_data.cc",
    "window_tree_data.h",
  ]

  if (use_ozone && !is_chromeos) {
    sources += [
      "mus_demo_external.cc",
      "mus_demo_external.h",
    ]
  } else {
    sources += [
      "mus_demo_internal.cc",
      "mus_demo_internal.h",
    ]
  }

  public_deps = [
    "//lib/services/service_manager/public/cpp",
    "//lib/skia",
  ]

  deps = [
    "//lib/base",
    "//lib/mojo/public/cpp/bindings",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/ui/public/cpp",
    "//lib/services/ui/public/cpp:internal",
    "//lib/services/ui/public/interfaces",
    "//kit/cpp/ui/aura",
    "//kit/cpp/ui/aura_extra",
    "//kit/cpp/ui/wm",
    "//lib/ui/gfx/geometry",
  ]
}

service("mus_demo") {
  if (use_ozone && !is_chromeos) {
    sources = [
      "main_external.cc",
    ]
  } else {
    sources = [
      "main_internal.cc",
    ]
  }

  deps = [
    ":lib",
  ]

  data_deps = [
    "//lib/services/ui",
  ]
}

group("demo") {
  deps = [
    ":mus_demo",
  ]
}

service_manifest("manifest") {
  name = "mus_demo"
  source = "manifest.json"
}

source_set("tests") {
  # testonly = true

  sources = [
    "mus_demo_unittests.cc",
  ]

  deps = [
    ":demo",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/service_manager/public/cpp:service_test_support",
    "//lib/services/ui/public/interfaces",
    "//lib/testing/gtest",
  ]

  data_deps = [
    ":mus_demo",
    "//lib/services/ui",
    "//lib/services/ui/ime/test_ime_driver",
  ]
}

service_manifest("unittest_manifest") {
  name = "mus_demo_unittests"
  source = "test_manifest.json"
}

catalog("tests_catalog") {
  # testonly = true
  embedded_services = [ ":unittest_manifest" ]
  standalone_services = [ ":manifest" ]
}
