# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//lib/services/catalog/public/tools/catalog.gni")
import("//lib/services/service_manager/public/cpp/service.gni")
import("//lib/services/service_manager/public/service_manifest.gni")
import("//lib/mojo/public/tools/bindings/mojom.gni")
import("//lib/testing/test.gni")

source_set("service_manager") {
  # testonly = true
  sources = [
    "service_manager_unittest.cc",
  ]

  deps = [
    ":interfaces",
    "//lib/base",
    "//lib/base/test:test_config",
    "//lib/mojo/edk",
    "//lib/mojo/public/cpp/system",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/service_manager/public/cpp:service_test_support",
    "//lib/services/service_manager/public/mojom",
    "//lib/services/service_manager/runner/common",
  ]

  data_deps = [
    ":service_manager_unittest_embedder",
    ":service_manager_unittest_target",
  ]
}

mojom("interfaces") {
  sources = [
    "service_manager_unittest.mojom",
  ]

  deps = [
    "//lib/services/service_manager/public/mojom",
  ]
}

service_manifest("manifest") {
  name = "service_manager_unittest"
  source = "service_manager_unittest_manifest.json"
}

executable("service_manager_unittest_target") {
  # testonly = true

  sources = [
    "target.cc",
  ]

  deps = [
    ":interfaces",
    "//lib/base",
    "//build/config:exe_and_shlib_deps",
    "//build/win:default_exe_manifest",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/service_manager/public/cpp/standalone_service:main",
  ]
}

service_manifest("target_manifest") {
  name = "service_manager_unittest_target"
  source = "target_manifest.json"
}

service_manifest("embedder_manifest") {
  name = "service_manager_unittest_embedder"
  source = "embedder_manifest.json"
  packaged_services = [
    ":all_users_manifest",
    ":regular_manifest",
    ":singleton_manifest",
  ]
}

service_manifest("all_users_manifest") {
  name = "service_manager_unittest_all_users"
  source = "all_users_manifest.json"
}

service_manifest("singleton_manifest") {
  name = "service_manager_unittest_singleton"
  source = "singleton_manifest.json"
}

service_manifest("regular_manifest") {
  name = "service_manager_unittest_regular"
  source = "regular_manifest.json"
}

service("service_manager_unittest_embedder") {
  # testonly = true

  sources = [
    "embedder.cc",
  ]

  deps = [
    ":interfaces",
    "//lib/base",
    "//build/win:default_exe_manifest",
    "//lib/mojo/edk",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/service_manager/public/cpp/standalone_service:main",
    "//lib/services/service_manager/public/mojom",
    "//lib/services/service_manager/runner:init",
    "//lib/services/service_manager/runner/common",
  ]
}

catalog("catalog") {
  embedded_services = [ ":manifest" ]

  standalone_services = [
    ":embedder_manifest",
    ":target_manifest",
  ]
}
