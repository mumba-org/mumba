# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

component("embedder") {
  public = [
    "embedded_service_info.h",
    "embedded_service_runner.h",
    "manifest_utils.h",
  ]

  sources = [
    "embedded_instance_manager.cc",
    "embedded_instance_manager.h",
    "embedded_service_info.cc",
    "embedded_service_runner.cc",
    "manifest_utils.cc",
    "service_manager_embedder_export.h",
  ]

  # iOS embeds the Service Manager but does not use service_manager::Main() (and
  # cannot use or even build it as-is).
  if (!is_ios) {
    public += [
      "main.h",
      "main_delegate.h",
      "process_type.h",
      "set_process_title.h",
      "set_process_title_linux.h",
      "shared_file_util.h",
    ]
    sources += [
      "main.cc",
      "main_delegate.cc",
      "set_process_title.cc",
      "set_process_title_linux.cc",
      "shared_file_util.cc",
    ]
  }

  if (is_mac) {
    sources += [
      "mac_init.h",
      "mac_init.mm",
    ]

    libs = [ "Foundation.framework" ]
  }

  if (is_win) {
    include_dirs = [
      "C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\Community\\VC\\Tools\\MSVC\\14.13.26128\\atlmfc\\include"
    ]
  }

  deps = [
    "//lib/base:base_static",
    "//lib/base:i18n",
  ]

  public_deps = [
    "//lib/base",
    "//lib/services/service_manager/public/cpp",
  ]

  if (!is_ios) {
    # deps of ServiceManager::Main and related functionality.
    deps += [
      "//lib/base/allocator:buildflags",
      "//lib/components/tracing:startup_tracing",
      "//lib/ui/base",
    ]

    public_deps += [
      ":embedder_switches",
      "//lib/mojo/edk",
      "//lib/services/service_manager/background:lib",
      "//lib/services/service_manager/public/cpp/standalone_service",
      "//lib/services/service_manager/runner:init",
      "//lib/services/service_manager/runner/common",
    ]
  }

  defines = [ "SERVICE_MANAGER_EMBEDDER_IMPL" ]
}

component("embedder_switches") {
  sources = [
    "service_manager_embedder_switches_export.h",
    "switches.cc",
    "switches.h",
  ]
  defines = [ "SERVICE_MANAGER_EMBEDDER_SWITCHES_IMPL" ]
}

source_set("unittests") {
  # testonly = true

  sources = [
    "embedded_instance_manager_unittest.cc",
    "manifest_utils_unittest.cc",
  ]

  # These headers are duplicated here so that they can remain private in the
  # "embedder" target. See http://crbug.com/732993 for a way to make it
  # unnecessary to do this.
  sources += [
    "embedded_instance_manager.h",
    "service_manager_embedder_export.h",
  ]

  deps = [
    ":embedder",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/testing/gtest",
  ]
}
