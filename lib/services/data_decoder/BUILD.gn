# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//lib/services/service_manager/public/cpp/service.gni")
import("//lib/services/service_manager/public/service_manifest.gni")
import("//lib/testing/libfuzzer/fuzzer_test.gni")

source_set("lib") {
  sources = [
    "data_decoder_service.cc",
    "data_decoder_service.h",
    "image_decoder_impl.cc",
    "image_decoder_impl.h",
    "json_parser_impl.cc",
    "json_parser_impl.h",
    "xml_parser.cc",
    "xml_parser.h",
  ]

  deps = [
    "//lib/base",
    "//lib/mojo/public/cpp/bindings",
    "//lib/skia",
    "//third_party/blink/public:blink",
    "//third_party/libxml",
    "//lib/ui/gfx",
    "//lib/ui/gfx/geometry",
  ]

  public_deps = [
    "//lib/services/data_decoder/public/mojom",
    "//lib/services/service_manager/public/cpp",
  ]
}

source_set("tests") {
  # testonly = true

  sources = [
    "image_decoder_impl_unittest.cc",
    "public/cpp/json_sanitizer_unittest.cc",
    "public/cpp/safe_xml_parser_unittest.cc",
    "public/cpp/testing_json_parser_unittest.cc",
    "xml_parser_unittest.cc",
  ]

  deps = [
    ":lib",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/gin",
    "//lib/gin:gin_test",
    "//lib/services/data_decoder/public/cpp",
    "//lib/services/data_decoder/public/cpp:test_support",
    "//lib/skia",
    "//lib/testing/gtest",
    "//third_party/blink/public:blink",
    "//tools/v8_context_snapshot",
    "//lib/ui/gfx",
  ]

  configs += [
    "//tools/v8_context_snapshot:use_v8_context_snapshot",
    "//v8:external_startup_data",
  ]
}

service_manifest("manifest") {
  name = "data_decoder"
  source = "manifest.json"
}

fuzzer_test("xml_parser_fuzzer") {
  sources = [
    "xml_parser_fuzzer.cc",
  ]
  deps = [
    ":lib",
    "//lib/base",
    "//third_party/libxml:libxml",
  ]
  dict = "//lib/testing/libfuzzer/fuzzers/dicts/xml.dict"
  seed_corpus = "xml_parser_fuzzer_corpus"
}
