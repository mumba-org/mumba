# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//lib/media/media_options.gni")

source_set("device_monitors") {
  # Do not expand the visibility here without double-checking with OWNERS, this
  # is a roll-up target which is part of the //media component. Most other DEPs
  # should be using //media and not directly DEP this roll-up target.
  visibility = [ "//lib/media" ]

  sources = [
    "device_monitor_mac.h",
    "device_monitor_mac.mm",
    "system_message_window_win.cc",
    "system_message_window_win.h",
  ]

  configs += [ "//lib/media:subcomponent_config" ]

  deps = [
    "//lib/base",
    "//lib/media/audio",
    "//lib/media/base",
  ]

  if (use_udev) {
    deps += [ "//lib/device/udev_linux" ]
    sources += [
      "device_monitor_udev.cc",
      "device_monitor_udev.h",
    ]
  }

  if (is_mac) {
    libs = [ "Foundation.framework" ]
  }
}

source_set("unit_tests") {
  # testonly = true
  sources = [
    "system_message_window_win_unittest.cc",
  ]

  deps = [
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/media:test_support",
    "//lib/testing/gmock",
    "//lib/testing/gtest",
  ]
}
