# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_component_build) {
  link_target_type = "source_set"
} else {
  link_target_type = "static_library"
}
target(link_target_type, "service") {
  visibility = [
    "//lib/components/viz/service/*",
    "//core/gpu:*",
    #"//content/public/gpu/*",
    "//lib/media/*",
  ]

  sources = [
    "gpu_video_decode_accelerator.cc",
    "gpu_video_decode_accelerator.h",
    "media_gpu_channel.cc",
    "media_gpu_channel.h",
    "media_gpu_channel_manager.cc",
    "media_gpu_channel_manager.h",
    "picture_buffer_manager.cc",
    "picture_buffer_manager.h",
    "vda_video_decoder.cc",
    "vda_video_decoder.h",
  ]

  include_dirs = [ "//third_party/mesa/src/include" ]

  public_deps = [
    "//lib/base",
    "//lib/gpu/config",
    "//lib/ipc",
    "//lib/media",
    "//lib/media/gpu",
  ]
  deps = [
    "//lib/gpu/command_buffer/service:gles2",
    "//lib/gpu/ipc/service",
    "//lib/media:media_buildflags",
    "//lib/media/gpu",
    "//lib/media/gpu:buildflags",
    "//lib/media/gpu/ipc/common",
    "//third_party/mesa:mesa_headers",
    "//lib/ui/gfx/ipc/color",
  ]

  if (is_win) {
    configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]
  }

  if (is_mac) {
    deps += [ "//third_party/webrtc/common_video:common_video" ]
  }
}

source_set("unit_tests") {
  # testonly = true
  sources = [
    "picture_buffer_manager_unittest.cc",
    "vda_video_decoder_unittest.cc",
  ]
  deps = [
    ":service",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/media:test_support",
    "//lib/testing/gmock",
    "//lib/testing/gtest",
  ]
}
