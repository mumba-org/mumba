# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//lib/components/viz/viz.gni")
import("//lib/testing/libfuzzer/fuzzer_test.gni")

viz_component("host") {
  defines = [ "VIZ_HOST_IMPLEMENTATION" ]

  sources = [
    "hit_test/hit_test_query.cc",
    "hit_test/hit_test_query.h",
    "host_frame_sink_client.h",
    "host_frame_sink_manager.cc",
    "host_frame_sink_manager.h",
    "renderer_settings_creation.cc",
    "renderer_settings_creation.h",
    "server_gpu_memory_buffer_manager.cc",
    "server_gpu_memory_buffer_manager.h",
    "viz_host_export.h",
  ]

  deps = [
    "//lib/base",
    "//lib/components/viz/common",
    "//lib/gpu/ipc/client",
    "//lib/gpu/ipc/common",
    "//lib/services/viz/privileged/interfaces",
    "//lib/ui/base",
    "//lib/ui/gfx",

    # TODO(kylechar): This is temporary and will be removed when all host to
    # service communication is over Mojo.
    "//lib/components/viz/service",
  ]

  public_deps = [
    "//lib/gpu/command_buffer/client",
    "//lib/gpu/ipc/host",
    "//lib/services/viz/privileged/interfaces/compositing",
    "//lib/services/viz/public/interfaces",
    "//lib/ui/gfx/geometry",
  ]
}

viz_source_set("unit_tests") {
  # testonly = true

  sources = [
    "hit_test/hit_test_query_unittest.cc",
    "host_frame_sink_manager_unittest.cc",
    "server_gpu_memory_buffer_manager_unittest.cc",
  ]

  deps = [
    ":host",
    "//lib/base",
    "//lib/base/test:test_support",
    "//lib/components/viz/test:test_support",
    "//lib/gpu/ipc/host",
    "//lib/mojo/public/cpp/bindings",
    "//lib/services/viz/privileged/interfaces",
    "//lib/services/viz/public/interfaces",
    "//lib/testing/gmock",
    "//lib/testing/gtest",

    # TODO(kylechar): This is temporary and will be removed when all host to
    # service communication is over Mojo.
    "//lib/components/viz/service",
  ]
}

fuzzer_test("hit_test_query_fuzzer") {
  sources = [
    "hit_test/hit_test_query_fuzzer.cc",
  ]

  libfuzzer_options = [ "max_len=4096" ]

  deps = [
    ":host",
    "//lib/base/test:test_support",
    "//lib/components/viz/test:test_support",
    "//lib/mojo/edk",
  ]
}
