# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//lib/services/catalog/public/tools/catalog.gni")
import("//lib/services/service_manager/public/cpp/service.gni")
import("//lib/services/service_manager/public/service_manifest.gni")
import("//lib/services/service_manager/public/tools/test/service_test.gni")
import("//lib/testing/test.gni")

static_library("lib") {
  sources = [
    "env_mojo.cc",
    "env_mojo.h",
    "leveldb_database_impl.cc",
    "leveldb_database_impl.h",
    "leveldb_mojo_proxy.cc",
    "leveldb_mojo_proxy.h",
    "leveldb_service_impl.cc",
    "leveldb_service_impl.h",
  ]

  public_deps = [
    "//lib/components/services/filesystem/public/interfaces",
    "//lib/components/services/leveldb/public/cpp",
    "//lib/components/services/leveldb/public/interfaces",
  ]

  deps = [
    "//lib/mojo/common",
    "//lib/mojo/public/cpp/system",
    "//lib/services/service_manager/public/cpp",
    "//third_party/leveldatabase",
  ]
}

service("leveldb") {
  sources = [
    "leveldb_app.cc",
    "leveldb_app.h",
    "main.cc",
  ]

  deps = [
    ":lib",
    "//lib/components/services/leveldb/public/interfaces",
    "//lib/mojo/common",
    "//lib/mojo/public/cpp/bindings",
    "//lib/mojo/public/cpp/system",
    "//lib/services/service_manager/public/cpp",
  ]
}

service_manifest("manifest") {
  name = "leveldb"
  source = "manifest.json"
}

service_test("leveldb_service_unittests") {
  sources = [
    "leveldb_mojo_unittest.cc",
    "leveldb_service_unittest.cc",
    "remote_iterator_unittest.cc",
  ]

  catalog = ":leveldb_service_unittests_catalog"

  deps = [
    "//lib/base",
    "//lib/components/services/filesystem/public/interfaces",
    "//lib/components/services/leveldb/public/cpp",
    "//lib/components/services/leveldb/public/interfaces",
    "//lib/mojo/common",
    "//lib/mojo/public/cpp/bindings",
    "//lib/mojo/public/cpp/system",
    "//lib/services/service_manager/public/cpp",
    "//lib/services/service_manager/public/cpp:service_test_support",
    "//third_party/leveldatabase",
  ]

  data_deps = [
    ":leveldb",
    "//lib/components/services/filesystem:filesystem",
  ]
}

service_manifest("test_manifest") {
  name = "leveldb_service_unittests"
  source = "test_manifest.json"
}

catalog("leveldb_service_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [
    ":manifest",
    "//lib/components/services/filesystem:manifest",
  ]
}
